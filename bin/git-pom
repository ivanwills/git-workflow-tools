#!/usr/bin/perl

# Created on: 2014-03-19 17:18:17
# Create by:  Ivan Wills
# $Id$
# $Revision$, $HeadURL$, $Date$
# $Revision$, $Source$, $Date$

use strict;
use warnings;
use Getopt::Long;
use Pod::Usage ();
use Data::Dumper qw/Dumper/;
use English qw/ -no_match_vars /;
use FindBin qw/$Bin/;
use Git::Workflow qw/config runner/;
use Git::Workflow::Pom qw/get_pom_versions pom_version next_pom_version/;

our $VERSION = 0.2;
my ($name)   = $PROGRAM_NAME =~ m{^.*/(.*?)$}mxs;

my %option = (
    pom     => config('workflow.pom') || 'pom.xml',
    fetch   => 1,
    verbose => 0,
    man     => 0,
    help    => 0,
    VERSION => 0,
);

main();
exit 0;

sub main {

    Getopt::Long::Configure('bundling');
    GetOptions(
        \%option,
        'pom|P=s',
        'update|u!',
        'fetch|f!',
        'verbose|v+',
        'man',
        'help',
        'VERSION!',
    ) or Pod::Usage::pod2usage(2);
    my $sub_command = shift @ARGV || 'uniq';

    if ( $option{'VERSION'} ) {
        print "$name Version = $VERSION\n";
        exit 1;
    }
    elsif ( $option{'man'} ) {
        Pod::Usage::pod2usage( -verbose => 2 );
    }
    elsif ( $option{'help'} ) {
        Pod::Usage::pod2usage( -verbose => 1 );
    }
    elsif ( !main->can($sub_command) ) {
        warn "Unknown sub command '$sub_command'!\n";
        Pod::Usage::pod2usage( -verbose => 1 );
    }

    $Git::Workflow::VERBOSE = $option{verbose};
    $Git::Workflow::TEST    = $option{test};

    # make sure that git is up-to-date
    runner(qw/git fetch/) if $option{fetch};

    my $versions = get_pom_versions($option{pom});

    main->$sub_command($option{pom}, $versions);

    return;
}

sub uniq {
    my (undef, $pom, $versions) = @_;
    my $numerical = my $version = pom_version($pom);
    $numerical =~ s/-SNAPSHOT$//xms;

    if ( !$versions->{$numerical} || keys %{ $versions->{$numerical} } <= 1 ) {
        print "POM Version $version is unique\n";
    }
    else {
        warn "Following branches are using version $numerical\n";
        warn "\t", join "\n\t", (sort keys %{ $versions->{$numerical} }), "\n";
        exit scalar keys %{ $versions->{$numerical} };
    }

    return;
}

sub next {
    my (undef, $pom, $versions) = @_;

    my $version = next_pom_version($option{pom}, $versions);
    print "$version\n";

    if ($option{update}) {
        runner(qw/mvn versions:set/, "â€“DnewVersion=$version");
    }

    return;
}

sub whos {
    my (undef, $pom, $versions) = @_;
    my $version = shift @ARGV;

    if (!$version) {
        warn "No version supplied!\n";
        Pod::Usage::pod2usage( -verbose => 1 );
    }

    $version =~ s/-SNAPSHOT$//;

    my $version_re = $version =~ /^\d+[.]\d+[.]\d+/ ? qr/^$version$/ : qr/^$version[.]\d+$/;
    my %versions = map {%{ $versions->{$_} }} grep {/$version_re/} keys %{ $versions };

    print join '', map {"$_\t$versions{$_}\n"} sort keys %versions;

    return;
}

__DATA__

=head1 NAME

git-pom - Manage pom.xml file versions

=head1 VERSION

This documentation refers to git-pom version 0.2

=head1 SYNOPSIS

   git-pom [uniq] [option]
   git-pom next [--update|-u]
   git-pom whos version [option]

 SUB-COMMAND:
  uniq              Confirm that the current branch is the only branch using it's version
  next              Calculates the next available version number
  whos              Which branch uses the pom version "version"

 OPTIONS:
  -u --update       Update to next version (used with next)

  -v --verbose      Show more detailed option
     --VERSION      Prints the version information
     --help         Prints this help information
     --man          Prints the full documentation for git-pom

=head1 DESCRIPTION

The C<git-pom> tool helps working with MAven POM files by looking at all branches to see
what versions are set. The sub commands allow different kinds of checking to be done.

=over 4

=item uniq

Check that the current branch's POM version is unique accross all branches.

=item next

Finds the next available POM version number bu finding the current nighest
POM version and incrementing the second number. If C<--update> is used then
the POM version is updated to that number.

=item whos

Find which branch or branches use a POM version number.

=back

=head1 SUBROUTINES/METHODS

=head1 DIAGNOSTICS

=head1 CONFIGURATION AND ENVIRONMENT

Defaults for this script can be set thought C<git config>

 workflow.prod  Sets how a prod release is determined
                eg the default equavilent is branch=^origin/master$
 workflow.pom   The default location for the pom.xml file (used C<--new-pom>
                and updating the pom.xml for the new branch)

You can set these values either by editing the repository local C<.git/config>
file or C<~/.gitconfig> or use the C<git config> command

 # eg Setting the global value
    git config --global workflow.prod 'branch=^origin/master$'

 # or set a repository's local value
    git config workflow.prod 'tag=^release_\d{4}_\d{2}\d{2}$'

 # or setting pom.xml location to a sub directory
    git config workflow.pom 'somedir/pom.xml'

=head1 DEPENDENCIES

=head1 INCOMPATIBILITIES

=head1 BUGS AND LIMITATIONS

There are no known bugs in this module.

Please report problems to Ivan Wills (ivan.wills@gmail.com).

Patches are welcome.

=head1 AUTHOR

Ivan Wills - (ivan.wills@gmail.com)

=head1 LICENSE AND COPYRIGHT

Copyright (c) 2014 Ivan Wills (14 Mullion Close, Hornsby Heights, NSW Australia 2077).
All rights reserved.

This module is free software; you can redistribute it and/or modify it under
the same terms as Perl itself. See L<perlartistic>.  This program is
distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
without even the implied warranty of MERCHANTABILITY or FITNESS FOR A
PARTICULAR PURPOSE.

=cut
